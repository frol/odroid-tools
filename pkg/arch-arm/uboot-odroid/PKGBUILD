#
# Package:    U-Boot for ODROID-X/X2/U2/U3
# Maintainer: Zoltan Tombol <zoltan dot tombol at gmail>
#
# Based on:   core/linux-armv7 (Arch Linux ARM)
#             by Kevin Mihelich <kevin@archlinuxarm.org>
#

buildarch=4

pkgbase=uboot-odroid
pkgname=('uboot-odroid' 'uboot-odroid-x')
pkgver=2016.09.01
pkgrel=1
arch=('armv7h')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
install="${pkgbase}.install"
backup=('boot/uEnv.txt')
makedepends=('bc' 'dtc')
_commit=dd9a970aa4accf5d266d334c0f319c674e933027
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl1.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl2.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/tzsw.HardKernel"
        'sd_fusing.sh'
        '0001-arch-linux-arm-modifications.patch'
        '0002-odroid-x-support.patch'
        'uEnv.txt')
md5sums=('61c628f8034477c946e173ed174efeb4'
         '3ab6d3cc2061bc2590d60320254017c6'
         '841502de02bd42f2898e36c89c260b0f'
         'c38faafa02a6a1ae834457f378c82113'
         '17909ae93a0ae7b4c155234040105035'
         'afe3b7a5b224e257b2ce7f5af03daa8d'
         'f4f8c8b6217e6646b9659de642264a3f'
         'fb5c9ea3108afa298087549f0453b00f')

prepare() {
  cd "u-boot-${pkgver}"

  patch -p1 -i "${srcdir}/0001-arch-linux-arm-modifications.patch"
}

build() {
  cd "u-boot-${pkgver}"

  unset CFLAGS
  unset CXXFLAGS
  unset CPPFLAGS

  # Build for X2/U2/U3.
  make distclean 
  make odroid_config
  make EXTRAVERSION="-${pkgrel}"
  mv u-boot-dtb.bin "${srcdir}/u-boot-dtb.bin"

  # Patch and build for X.
  patch -p1 -i "${srcdir}/0002-odroid-x-support.patch"
  make distclean
  make odroid_config
  make EXTRAVERSION="-${pkgrel}"
}

package_uboot-odroid() {
  pkgdesc='U-Boot for ODROID-X2/U2/U3'

  cd "${srcdir}"

  mkdir -p "${pkgdir}/boot"
  cp u-boot-dtb.bin "${pkgdir}/boot/u-boot.bin"
  cp {{bl1,bl2,tzsw}.HardKernel,sd_fusing.sh} "${pkgdir}/boot"
  cp uEnv.txt "${pkgdir}/boot"
}

package_uboot-odroid-x() {
  pkgdesc='U-Boot for ODROID-X'

  cd "${srcdir}"

  mkdir -p "${pkgdir}/boot"
  cp "u-boot-${pkgver}/u-boot-dtb.bin" "${pkgdir}/boot/u-boot.bin"
  cp {{bl1,bl2,tzsw}.HardKernel,sd_fusing.sh} "${pkgdir}/boot"
  cp uEnv.txt "${pkgdir}/boot"
}
